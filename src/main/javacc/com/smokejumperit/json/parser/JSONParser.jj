
options {
	CHOICE_AMBIGUITY_CHECK=3;
	OTHER_AMBIGUITY_CHECK=2;
	//DEBUG_PARSER=true
	//DEBUG_LOOKAHEAD=true
	//DEBUG_TOKEN_MANAGER=true
	ERROR_REPORTING=true;
	JAVA_UNICODE_ESCAPE=true;
	UNICODE_INPUT=true;
	IGNORE_CASE=true;
	SUPPORT_CLASS_VISIBILITY_PUBLIC=false;
	FORCE_LA_CHECK=true;
	CACHE_TOKENS=true;
  SANITY_CHECK = true;
	STATIC=false;
  //KEEP_LINE_COLUMN=true;
}

PARSER_BEGIN(JSONParser)

package com.smokejumperit.json.parser;

import java.io.*;
import java.util.*;
import java.math.*;

/**
* Basic JSON parser generated by JavaCC. It consumes the input provided through the constructor when 
* {@code parseObject()}, {@code parseList()}, or {@code parse()} are called, and there is no way to directly
* reset the state.
* Set the {@code fallbackToString} property if you want to enable
* unparseable constructs (such as unqouted strings) to be handled as Strings: otherwise, they are a parse
* error.
*
* <p />
*
* This class makes no pretenses towards being threadsafe.
*/
public class JSONParser {

	private boolean nativeNumbers = false;

	public JSONParser(String input) {
    this(new StringReader(input));
  }

	/**
	* Parses a JSON object into a Java {@code Map}.
	*/
	public LinkedHashMap<String,Object> parseObject() throws ParseException { 
		LinkedHashMap<String,Object> toReturn = object(); 
		if(!ensureEOF()) throw new IllegalStateException("Expected EOF, but still had content to parse");
		return toReturn;
	}

	/**
	* Parses a JSON array into a Java {@code List}.
	*/
	public ArrayList<Object> parseArray() throws ParseException {	
		ArrayList<Object> toReturn = list(); 
		if(!ensureEOF()) throw new IllegalStateException("Expected EOF, but still had content to parse");
		return toReturn;
	}

	/**
	* Parses any JSON-parseable object, returning the value. 
	*/
	public Object parse() throws ParseException { 
		Object toReturn = anything(); 
		if(!ensureEOF()) throw new IllegalStateException("Expected EOF, but still had content to parse");
		return toReturn;
	}

	private static String substringBefore(String str, char delim) {
		int pos = str.indexOf(delim);
		if(pos == -1) return str;
		return str.substring(0, pos);
	}

	public void setNativeNumbers(boolean value) {
		this.nativeNumbers = value;
	}

	public boolean getNativeNumbers() {
		return this.nativeNumbers;
	}

}

PARSER_END(JSONParser)

// Ignore comments
SKIP: {
	<C_SINGLE_COMMENT: "//" (~["\n","\r","\f"])* <EOL>>
| <C_MULTILINE_COMMENT: "/*" (~[])* "*/">
| <SH_SINGLE_COMMENT: "#" (~["\n","\r","\f"])* <EOL>>
| <WHITESPACE: " " | "\t"> 
| <EOL: "\n" | "\r" | "\f"> 
}

// Common tokens
TOKEN: {
	<COMMA: ",">
}

// Object tokens
TOKEN:{
	<BRACE_OPEN: "{">
| <BRACE_CLOSE: "}">
| <COLON: ":">
}

// Array tokens
TOKEN:{
	<BRACKET_OPEN: "[">
| <BRACKET_CLOSE: "]">
}

// Number token
TOKEN:{
	<#ZERO: "0">
| <#DIGIT_NONZERO: ["1"-"9"]>
| <#DIGIT: (<DIGIT_NONZERO> | <ZERO>) >
| <NUMBER_INTEGER:
		("-")?
		( (<ZERO>)+ | ( <DIGIT_NONZERO> (<DIGIT>)* ) ) 
	>
| <NUMBER_DECIMAL: 
		("-")?  
		( (<ZERO>)+ | ( <DIGIT_NONZERO> (<DIGIT>)* ) ) 
		("." 
			(<DIGIT>)+  
			(
				["e","E"]
				("+" | "-")?
				(<DIGIT>)+
			)?
		)
	>
}

// Boolean tokens
TOKEN:{
	<TRUE: "true">
| <FALSE: "false">
}

// Null token
TOKEN:{
	<NULL: "null">
}

// String tokens
TOKEN:{
	<#QUOTE_DOUBLE: "\"">
| <#QUOTE_SINGLE: "'">
| <STRING_SINGLE_EMPTY: "''">
| <STRING_DOUBLE_EMPTY: "\"\"">
| <#STRING_SINGLE_BODY: (
		(~["'","\\","\r","\n","\f","\t"]) |
		( "\\" ( "r" | "n" | "f" | "\\" | "/" | "'" | "b" | "t" ) )
	)+>
| <#STRING_DOUBLE_BODY: (
		(~["\"","\\","\r","\n","\f","\t"]) |
		( "\\" ( "r" | "n" | "f" | "\\" | "/" | "\"" | "b" | "t" ) )
	)+>
| <STRING_SINGLE_NONEMPTY: <QUOTE_SINGLE> <STRING_SINGLE_BODY> <QUOTE_SINGLE>>
| <STRING_DOUBLE_NONEMPTY: <QUOTE_DOUBLE> <STRING_DOUBLE_BODY> <QUOTE_DOUBLE>>
| <RUBY_SYMBOL:  ":" (["a"-"z"] | ["A"-"Z"] | ["0"-"9"] | "_")+>
}

// Raw symbol tokens
TOKEN:{
	<SYMBOL: (["a"-"z", "A"-"Z", "0", "1"-"9"])+ >
}


boolean ensureEOF() : {}{
	<EOF>
	{ return true; }
}

Object anything() : { 
	Object x;
}{
	( x = object()
	| x = list()
	| x = value()
	)
	{ return x; }
}

String objectKey() : {
	Object o;
	String key;
} {
	(
		key = string()
	| key = symbol()
	| ( 
		nullValue()
		{ key = null; }
		)
	| (
			( o = booleanValue() | o = number() )
			{ key = o.toString(); }
		)
	)
	{ return key; }
}

LinkedHashMap<String,Object> object() : {
	final LinkedHashMap<String,Object> map = new LinkedHashMap<String,Object>();
	String key;
	Object value;
}{
	<BRACE_OPEN>
	[
		key = objectKey() 
		<COLON>
		value = anything()
		{ map.put(key, value); }
		{ key = null; value = null; }
		(
			<COMMA>
			key = objectKey()
			<COLON>
			value = anything()
			{ map.put(key, value); }
			{ key = null; value = null; }
		)*
	]
	<BRACE_CLOSE>
	{ return map; }
}

ArrayList<Object> list() : {
	final ArrayList<Object> list = new ArrayList<Object>(); 
	Object value;
}{
	<BRACKET_OPEN>
	[
		value = anything()
		{ list.add(value); }
		{ value = null; }
		(
			<COMMA>
			value = anything()
			{ list.add(value); }
			{ value = null; }
		)*
	]
	<BRACKET_CLOSE>
	{ 
		list.trimToSize();
		return list; 
	}
}

Object value() : {
	Object x;
}{
	( x = string()
	| x = number()
	| x = booleanValue()
	| x = nullValue()
	)
	{ return x; }
}

Object nullValue(): {}{
	<NULL>
	{ return null; }
}

Boolean booleanValue(): {
	Boolean b;
}{
	(
		(
			<TRUE>
			{ b = Boolean.TRUE; }
		) | (
			<FALSE>
			{ b = Boolean.FALSE; }
		)	
	)
	{ return b; }
}

Number number(): {
	Token t;
}{ 
	(
		t = <NUMBER_DECIMAL>
		{ 
			if(nativeNumbers) {
				return new Long(t.image);
			} else {
				return new BigDecimal(t.image); 
			}
		}
	) | (
		t = <NUMBER_INTEGER>
		{ 
			if(nativeNumbers) {
				return new Double(t.image);
			} else {
				return new BigInteger(substringBefore(t.image, '.')); 
			}
		}
	)
}

String string() : {
	String s;
}{
	( s = doubleQuoteString() 
	| s = singleQuoteString()
	| s = rubySymbol()
	)
	{ return s; }
}

String rubySymbol(): {
}{
	<RUBY_SYMBOL>
	{ return t.image.substring(1); }
}

String doubleQuoteString() : {
	Token t;
}{
	(
		<STRING_DOUBLE_EMPTY>
		{ return ""; }
	) | (
		t = <STRING_DOUBLE_NONEMPTY>
		{ return t.image.substring(1, t.image.length()-1); }
	)
}

String singleQuoteString() : {
	Token t;
}{
	(
		<STRING_SINGLE_EMPTY>
		{ return ""; }
	) | (
		t = <STRING_SINGLE_NONEMPTY>
		{ return t.image.substring(1, t.image.length()-1); }
	)
}

String symbol() : {
	Token t;
}{
	t = <SYMBOL>
	{ return t.image; }
}
